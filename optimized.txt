10 REM snake by Or Galil
20 CLEAR:CLS:DIM SC(32,4):LE=3:C=236:U$="3"
30 REM custom characters
40 FOR A=240 TO 247:READ A$:DEFCHR$(A)=A$:NEXT
50 SX=2:SY=0:TX=0:TY=0
60 REM snake setup
70 SC(SX-1,SY)=247:SC(SX-2,SY)=247
80 PRINTCHR$(247);CHR$(237);CHR$(242)
90 GOSUB 430
100 REM user input
110 REM PT=VAL(RIGHT$(TIME$,2))
120 REM IF PT=VAL(RIGHT$(TIME$,2)) THEN 120
130 PU$=U$:U$=INKEY$
140 BX=SX:BY=SY
150 ON VAL(U$) GOTO 200,220,190,160,210
160 U$=PU$
170 GOTO 150
180 REM directions
190 IF SX+1>31 THEN GOTO 500 ELSE SX=SX+1:HC=242:TC=247:GOTO 230
200 IF SX-1<0 THEN GOTO 500 ELSE SX=SX-1:HC=243:TC=246:GOTO 230
210 IF SY-1<0 THEN GOTO 500 ELSE SY=SY-1:HC=241:TC=244:GOTO 230
220 IF SY+1>3 THEN GOTO 500 ELSE SY=SY+1:HC=240:TC=245:GOTO 230
230 REM move
240 IF SC(SX,SY)<>0 THEN IF SC(SX,SY)=233 THEN LE=LE+1:AA=1 ELSE GOTO 500 ELSE GOSUB 340
250 GOSUB 290
260 LOCATE SX,SY:PRINTCHR$(HC);:SC(BX,BY)=TC
270 IF AA=1 THEN AA=0:GOSUB 430
280 GOTO 100
290 REM print body
300 LOCATE BX,BY:PRINTCHR$(C);
310 IF C=236 THEN C=237:RETURN
320 IF C=237 THEN C=236:RETURN
330 RETURN
340 REM move tail
350 LOCATE TX,TY:PRINT" ";
360 ON SC(TX,TY)-243 GOSUB 390,400,410,420
370 LOCATE TX,TY:PRINTCHR$(SC(TX,TY));
380 RETURN
390 SC(TX,TY)=0:TY=TY-1:RETURN
400 SC(TX,TY)=0:TY=TY+1:RETURN
410 SC(TX,TY)=0:TX=TX-1:RETURN
420 SC(TX,TY)=0:TX=TX+1:RETURN
430 REM generate apple
440 BEEP1
450 AX=INT(RND(-1)*31)
460 AY=INT(RND(-1)*3)
470 IF SC(AX,AY)<>0 THEN 450
480 LOCATE AX,AY:PRINTCHR$(233);:SC(AX,AY)=233
490 RETURN
500 REM ending
510 CLS:PRINT "LENGTH:";LE
520 BEEP:BEEP1:BEEP:END
530 REM char data
540 DATA "E0B8FCB8E000","071D3F1D0700","7C547C383810","1038387C547C"
550 DATA "00787E780000","001E7E1E0000","003838381010","101038383800"
